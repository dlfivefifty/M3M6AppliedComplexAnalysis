__M3M6: Applied Complex Analysis (2020)__

Dr. Sheehan Olver

s.olver@imperial.ac.uk

Office Hours: 11am Tuesdays, Huxley 6M40

Website: https://github.com/dlfivefifty/M3M6AppliedComplexAnalysis

References: 

1. M.J. Ablowitz & A.S. Fokas, Complex Variables: Introduction and Applications, Second Edition, Cambridge University Press, 2003


## Overview of course


1. Complex analysis, Cauchy's theorem, residual calculus
3. Singular integrals of the form
$$\int_\Gamma {u(\zeta) \over z - \zeta}  d\zeta,$$
$$\int_\Gamma u(\zeta) \log|z - \zeta|  ds$$
with applications to PDEs, airfoil design, etc.
2. Weiner–Hopf method with applications to integral equations with integral operators
$$\int_0^\infty K(x-y) u(y)  dy$$
4. Orthogonal polynomials, with applications to Schrödinger operators, solving differential equations.
5. Conformal mapping and Schwartz–Christofel maps. 

_Central themes_: 

1. Finding "nice" formulae for problems that arise in applications (physics and elsewhere). These can be closed form solution, sums, integral representations, special functions, etc.
2. Computational tools for approximate solutions to problems that arise in applications.


_Applications_ (not necessarily discussed in the course):

1. Ideal fluid flow
2. Acoustic scattering
3. Electrostatics (Faraday cage)
4. Fracture mechanics
5. Schrödinger equations
6. Shallow water waves

## The Project

There is a project worth 10%. This project is _open ended_: you propose a topic. This could be computational based (possibly based on the slides), theoretical based (possibly looking at material from Ablowitz & Fokas), or otherwise. If you are having difficulty coming up with a proposal, please attend the office hours for advice.

Timeline:

* 11 Feb: Office hours to discuss potential projects
* 20 Feb: Turn in short description of proposed project (max 2 paragraphs)
* 20 March: Project due

## Problem sheets

There will be 4 problem sheets during the term. 

## Feedback on course work

I'll set aside four office hours to walk 
through individual solutions to the problem sheets on a 1-on-1 basis.
Please email me ahead of time so I can schedule. 




# Lecture 1: Complex analysis reivew

The first  couple lectures will review the basics of complex analysis. 
We will use plots to help explain the material, and so this first lecture will focus on getting comfortable with plots of analytic functions.

## Plotting functions in the complex plane


Consder a complex-valued function $f : D \rightarrow {\mathbb C}$ where $D \subset {\mathbb C}$. To help understand such functions it is useful to plot them.  But how?

__Method 1: real and imaginary parts__

Every complex-valued function can be written as $f(z) = u(z) + i v(z)$ where $u : D \rightarrow {\mathbb R}$ and $v : D \rightarrow {\mathbb R}$ are real-valued. These are easy to plot separately. We can do a surface plot:
```julia
using Plots, ComplexPhasePortrait, SpecialFunctions

f = z -> exp(z)
u = z -> real(f(z))
v = z -> imag(f(z))

# set up plotting grid
xx = range(-2 ; stop=2,  length=100)
yy = range(-10; stop=10, length=100)

plot(surface(xx, yy, u.(xx' .+ im.*yy); title="real"),
     surface(xx, yy, v.(xx' .+ im.*yy); title="imag"))
```

Or a heat plot:
```julia
plot(contourf(xx, yy, u.(xx' .+ im.*yy); title="real"),
     contourf(xx, yy, v.(xx' .+ im.*yy); title="imag"))
```     

__Method 2: absolute-value and angle__

Every complex number $z$ can be written as $re^{i \theta}$ for $0 \leq r$ and $-\pi < \theta \leq \pi$. $r = |z|$ is called the _absolute value_ and $\theta$ is called the _phase_ or _argument_ (or in Julia, _angle_).  We can plot these:

```julia
xx = -2:0.01:2
yy = -10:0.01:10

f = z -> exp(z)

r = z -> abs(f(z))
θ = z -> angle(f(z))


plot( contourf(xx, yy, r.(xx' .+ im.*yy); title="abs"),
      contourf(xx, yy, θ.(xx' .+ im.*yy); title="phase"))
```

__ Method 3: Phase portrait __

This method is essentially the same, as before, but to _only_ plot the phase, and use a "colour wheel" to reflect the topology of $\theta$. 
```julia
phaseplot(-3..3, -8..8, z -> exp(z))
```
This is seen most clearly with $f(z) = z$: in this case, letting $z = r \E^{\I \theta}$ we are simply plotting $\theta$:
```julia
phaseplot(-3..3, -3..3, z -> z)
```

In other words, the colour red corresponds to $\arg f(z) \approx 0$, green to $\arg f(z) \approx {2 \pi \over 3}$ aqua to $\arg f(z) \approx \pi$ and so on. 


Note that multiplying $z$ by a complex number $R \E^{\I \varphi}$ will rotate the wheel, but the colours still appear in the same order when read counter clockwise:

```julia
phaseplot(-3..3, -3..3, z -> exp(1.0im)*z)
```

Therefore, if $f(z)$ has a zero at $z_0$, since it behaves like $f(z) = f'(z_0) (z - z_0)$, we will have the full colour wheel always in the counter clockwise order red–green–blue–red:
```julia
phaseplot(-20..20, -3..3, z -> sin(z))
```

In the case of a double root like $f(z) = z^2 = r \E^{2 \I \theta}$, we have the wheel appearing twice, but still in the same order. Thus the order of a zero can be seen by the number of times we go around the colour wheel: here we see that the function has a triple root at zero since it goes red–green–blue–red–green–blue–red–green–blue–red:
```julia
phaseplot(-5..5, -3..3, z -> z^2*sin(z))
```

On the other hand, if we plot $f(z) = z^{-1} = r^{-1} \E^{-\I \theta}$ the wheel is reversed to  be red–blue–green–red:

```julia
phaseplot(-3..3, -3..3, z -> 1/z)
```

Thus we can see from a phase plot where the poles and zeros are, and the order of the poles and roots: the following function has a double pole at 0 (red–blue–green–red–blue–green), a zero at $-{\pi \over 2}$ (red–green–blue–red) and a double zero at $\pi\over 2$ (red–green–blue–red–green–blue–red) :

```julia
phaseplot( -3..5, -3..3, z -> cot(z)/z*(π/2-z))
```

Functions with more complicated singularities do not have such nice phase plots (these are what we will call _essential_ singularities):

```julia
phaseplot(-3..3, -3..3, z -> exp(1/z))
```

The jumps of functions like $\log z$ or $\sqrt z$ also appear naturally in the phase plot: here we see $\log z$ has a zero at $1$ (red–green–blue–red) and a jump along $(-\infty,0]$

```julia
phaseplot( -3..3, -3..3, z -> log(z))
```

Often, singularities are in the complex plane. can you determine where the zeros and roots, and their orders, of the following function are from the picture?

```julia
phaseplot(-3..3, -3..3, z -> z^5  / (1 + z^2)^3)
```

```julia
phaseplot(-3..3, -3..3, z -> erfc(z))
```

Here's the Gamma function. Are they poles or zeros at 0, -1, -2, ...?

```julia
phaseplot(-3..3, -3..3, z -> gamma(z))
```

Something strange is happening at -1/2, -3/2, .... What if we differentiate? Here `dual(z,1)` is a [dual number](https://en.wikipedia.org/wiki/Dual_number), which provides a convenient way to implement automatic differentiation: `gammap(z)` is derivative of `gamma(z)` w.r.t. `z`.

```julia
using DualNumbers
gammap = z -> epsilon(gamma(dual(z,1)))
phaseplot(-3..3, -3..3, gammap) 
```

Finally, the Riemann Hypothesis: Here is a plot of the Riemann zeta function near the critical line $z = 0.5 + \I t$:
```julia
phaseplot(0:0.011:2, -2:0.011:40, zeta)
```


## Holomorphicity, Cauchy's theorem, and Analyticity

A beautiful feature of complex analysis is one starts with a very
weak assumption of holomorphicity, essentially
complex-differentiability
in an open set, deduce Cauchy's theorem and Cauchy's integral formula,
which then imply analyticity. That is, if we assume a function
is one-time complex differentiable, then it has an infinite number
of derivatives.

These results are built up as follows:

1. Holomorphic functions
2. Cauchy's theorem 
3. Deformation of contours
4. Cauchy's integral formula
5. Analyticity and Taylor series

We will review the first three topics this lecture, with the remaining for next lecture.

###  Holomorphic functions 


**Definition (Complex-differentiable)** Let $D \subset {\mathbb C}$ be an open set.  A function $f : D \rightarrow {\mathbb C}$ is called _complex-differentiable_ at a point $z_0 \in D$ if 
$$
   f'(z_0) =  \lim_{z \rightarrow z_0} {f(z) - f(z_0) \over z - z_0}
$$
exists, for any angle of approach to $z_0$.


**Definition (Holomorphic)** Let $D \subset {\mathbb C}$ be an open set.  A function $f : D \rightarrow {\mathbb C}$ is called _holomorphic_ in $D$ if it is complex-differentiable at all $z \in D$.

**Definition (Entire)** A function is _entire_ if it is holomorphic in ${\mathbb C}$

*Examples*

1. $1$ is entire
2. $z$ is entire
3. $1/z$ is holomorphic in ${\mathbb C} \backslash \{0\}$
4. $\sin z$ is entire
5. $\csc z$ is holomorphic in ${\mathbb C} \backslash \{\ldots,-2\pi,-\pi,0,\pi,2\pi,\ldots\}$
6. $\sqrt z$ is holomorphic in ${\mathbb C} \backslash (-\infty,0]$

### Cauchy's theorem

**Definition (Contour)** A _contour_  is a continuous & piecewise-continuously differentiable function $\gamma : [a,b] \rightarrow {\mathbb C}$.

**Definition (Simple)** A _simple contour_ is a contour that is 1-to-1.

**Definition (Closed)** A _closed contour_ is a contour such that $\gamma(a) = \gamma(b)$

*Examples of contours*

1. Line segment $[a,b]$ is a simple contour, with $\gamma(t) =  t$
1. Arc from $re^{ia}$ to $re^{ib}$ is a simple contour, with $\gamma(t) =  re^{i t}$
2. Circle of radius $r$ is a closed simple contour, with $\gamma(t) = re^{i t}$ and $a = -\pi$, $b = \pi$
3. $\gamma(t) = \cos (t+i)^2$ defines a contour that is not simple or closed
4. $\gamma(t) = e^{i t} + e^{2i t}$ for $[a,b] = [-\pi,\pi]$ defines a contour that is closed but not simple


**Definition (Contour integral)** The _contour integral_ over $\gamma$ is defined by
$$
\int_\gamma f(z) dz := \int_a^b f(\gamma(t)) \gamma'(t) dt
$$

An important property of a contour is its _arclength_:

**Definition (Arclength)** The _arclength_ of $\gamma$ is defined as
$$
    {\cal L}(\gamma) := \int_a^b |\gamma'(t)| dt
$$

A very useful result is that we can use the maximum and arclength to bound integrals:

**Proposition (ML)** Let $f : \gamma \rightarrow {\mathbb C}$ and 
$$
        M = \sup_{z \in \gamma} |f(z)|
$$
Then
$$
\left|\int_\gamma f(z) dz \right| \leq M {\cal L}(\gamma)
$$


**Proposition** If $f(z)$ is holomorphic on $\gamma$, then
$$\int_\gamma f'(z) dz = f(\gamma(b)) - f(\gamma(a))$$

**Theorem (Cauchy)** If $f$ is holomorphic inside and on a closed contour $\gamma$, then 
$$\oint_\gamma f(z) dz = 0$$

### Deformation of contours

**Definition (Domain)** A _domain_ is a non-empty, open and connected set $D \subset {\mathbb C}$.

**Definition (Homotopic)** Two closed contours $\gamma_1 : [a,b] \rightarrow D$ and $\gamma_2 \rightarrow D$ in a domain $D$ are _homotopic_ if they can be continuously deformed to one-another while remaining in $D$. 

**Theorem (Deformation of contours)** Let $f(z)$ be holomorphic in a domain $D$. Let $\gamma_1$ and $\gamma_2$ be two homotopic contours. Then
$$
    \int_{\gamma_1} f(z) dz = \int_{\gamma_2} f(z) dz
$$

**Definition (Simply connected)** A domain is _simply connected_ if every closed contour is homotoic to a point.

**Corollary (Deformation of contours on simply connected domains)** Let $f(z)$ be holomorphic in a simply-connected domain $D$. If $\gamma_1$ and $\gamma_2$ are two contours in $D$ with the same endpoints, then

$$
    \int_{\gamma_1} f(z) dz = \int_{\gamma_2} f(z) dz
$$



### Demonstration 

We can usually infer the domain where a function is holomorphic from a phase portrait, here we see that ${\rm arcsinh}\, z$ has cuts on $[\I,\I \infty)$ and $[-\I,-\I \infty)$, and a zero  (red–green–blue–red) at zero, hence we can infer that it is holomorphic in $\C \backslash ([\I,\I \infty) \cup [-\I,-\I \infty))$.

```julia
using Plots, ComplexPhasePortrait, SpecialFunctions, ApproxFun
phaseplot(-4..4, -4..4, z -> asinh(z))
```

The following example $\sqrt{z-1} \sqrt{z+1}$ is analytic in ${\mathbb C}\backslash [-1,1]$ and will be returned to:

```julia
phaseplot(-4..4, -4..4, z -> sqrt(z-1)sqrt(z+1))
```

Here's an example of a closed contour that is not simple:

```julia
a,b = -π, π
tt = range(a, stop=b, length=1000)

γ = t -> exp(im*t) +exp(2im*t)

plot(real.(γ.(tt)), imag.(γ.(tt)); ratio=1.0, legend=false, arrow=true)
```

The notion of integration over a contour in the complex plane 
is independent of the notion of holomorphicity. For example, we
can happily define the integral for non-holomorphic functions such as
$\Re \E^z = \E^x \cos y$:

```julia
f = Fun( z -> real(exp(z)), Arc(0.,1.,(0,π/2)))  # Not holomorphic!

plot(domain(f); legend=false, ratio=1.0, arrow=true)
```
```julia
sum(f)  # this means contour integral
```
This is defined in terms of a parameterization, and so
the following real integral gives the same result:
```julia
g = im*Fun(t-> f(exp(im*t))*exp(im*t), 0 .. π/2)
sum( g )  # this is standard integral
```

The following show that the contour of integration does not affect the integral for holomorphic functions:

```julia
f = Fun( z -> exp(z), Arc(0.,1.,(0,π/2))) #integrate over an arc
sum(f)  , f(im)-f(1)
```

```julia
f = Fun( z -> exp(z), Segment(1,im)) # integrate over a line segment
sum(f)  , f(im)-f(1)
```

The following demonstrates Cauchy's theorem, integration over a closed
contour equals zero:
```julia
f = Fun( z -> exp(z), Circle())  #  Holomorphic!
sum(f)
```
If `f` is not holomorphic, it doesn't apply:
```julia
f = Fun( z -> exp(z)/z, Circle())  # Not holomorphic at zero
sum(f)
```
```julia
f = Fun( z -> real(exp(z)), Circle())  # Not holomorphic anywhere!
sum(f)
```

We can test contour deformation experimentally: in the following, the 
integral (implemented as `sum`) over two different contours returns the 
same value (up to numerical precision)
```julia
f = Fun( z -> exp(z), Arc(0.,1.,(0,π/2)))  # Holomorphic!
fs = Fun( z -> exp(z), Segment(1,im))  # Holomorphic!

sum(f)  , sum(fs)
```

Here is a depection of the two contours:

```julia
plot(domain(f); ratio=1.0, legend=false, arrow=true)
plot!(domain(fs), arrow=true)
```